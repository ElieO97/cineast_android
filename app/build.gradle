apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply from: "../ktlint.gradle"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "elieomatuku.cineast_android"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode = rootProject.ext.versionCode
        versionName = rootProject.ext.versionName
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        def apiKeyPropertiesFile = rootProject.file("apikeys.properties.txt");
        if (apiKeyPropertiesFile.exists()) {
            def apiKeyProperties = new Properties()
            apiKeyProperties.load(new FileInputStream(apiKeyPropertiesFile))
            buildConfigField("String", "api_key", apiKeyProperties['api_key'])
        } else {
            buildConfigField("String", "api_key", "\"${System.env.API_KEY}\"")
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        dev {
            applicationIdSuffix = ".dev"
            resValue "string", "app_name", (rootProject.ext.appName + "_dev")
            resValue "string", "leak_canary_display_activity_label", (rootProject.ext.appName + "_dev_leaks")
        }

        beta {
            applicationIdSuffix = ".beta"
            resValue "string", "app_name", (rootProject.ext.appName + "_beta")
            resValue "string", "leak_canary_display_activity_label", (rootProject.ext.appName + "_beta_leaks")
        }

        prod {
            resValue "string", "app_name", rootProject.ext.appName
            resValue "string", "leak_canary_display_activity_label", (rootProject.ext.appName + "_leaks")
        }
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "version_name", variant.versionName
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        dataBinding = true
    }

}

androidExtensions {
    experimental = true
}

repositories {
    maven { url "https://jitpack.io" }
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url "https://oss.sonatype.org/discoverContent/repositories/snapshots" }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Local modules
    implementation project(':cache')
    implementation project(':domain')

    implementation 'androidx.multidex:multidex:2.0.1'

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.1'

    //ui
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.wang.avi:library:2.1.3'
    implementation 'com.github.PierfrancescoSoffritti:AndroidYouTubePlayer:7.0.0'
    implementation 'info.hoang8f:android-segmented:1.0.6'
    implementation 'me.zhanghai.android.materialratingbar:library:1.3.1'
    implementation 'com.eftimoff:android-viewpager-transformers:1.0.1@aar'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    // network
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation 'com.squareup.okio:okio:2.10.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'

    implementation "androidx.lifecycle:lifecycle-livedata-core-ktx:${versions.lifecycle}"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:${versions.lifecycle}"
    implementation "androidx.lifecycle:lifecycle-common-java8:${versions.lifecycle}"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${versions.lifecycle}"

    // rxjava2
    implementation 'io.reactivex.rxjava2:rxjava:2.2.9'
    implementation 'com.jakewharton.rxbinding2:rxbinding-kotlin:2.2.0'

    // mvp
    implementation 'com.github.jhavatar:mythos:1.0.5'

    //dependency
    implementation 'org.kodein.di:kodein-di-generic-jvm:6.4.0'

    // support
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    //dev utils
    implementation 'com.github.ajalt:timberkt:1.4.0'

    //other
    implementation 'com.squareup.moshi:moshi:1.8.0'

    //flatcircle coroutine helper
    implementation 'io.flatcircle:coroutinehelper:0.0.3'

    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:2.3.1"
    implementation "androidx.activity:activity-ktx:1.2.3"
    implementation "androidx.fragment:fragment-ktx:1.3.3"


    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.3'


    // test
    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.test:runner:1.3.0'
    testImplementation 'androidx.test:rules:1.3.0'
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "org.robolectric:robolectric:4.3.1"
    testImplementation 'org.mockito:mockito-core:2.23.4'
    testImplementation 'com.github.gmazzo:okhttp-mock:1.3.1'
    testImplementation 'io.mockk:mockk:1.9'
    testImplementation 'io.mockk:mockk-android:1.9'

    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'org.mockito:mockito-android:2.23.4'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
    implementation 'androidx.test.espresso:espresso-idling-resource:3.3.0'
}
